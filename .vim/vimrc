"--------------------------------------------------------------;
"                          Vim-Plug                            ;
"--------------------------------------------------------------;

call plug#begin()

"----------------------------------;
" Session handling                 ;
"----------------------------------;

Plug 'mhinz/vim-startify'
let g:startify_session_persistence = 1
let g:startify_session_dir = '~/.vim/session'
let g:startify_lists = [
\ { 'type': 'sessions',  'header': ['   Sessions'      ] },
\ { 'type': 'files',     'header': ['   MRU'           ] },
\ { 'type': 'dir',       'header': ['   MRU '. getcwd()] },
\ { 'type': 'bookmarks', 'header': ['   Bookmarks'     ] },
\ { 'type': 'commands',  'header': ['   Commands'      ] },
\ ]

"----------------------------------;
" Useful general usage plugins     ;
"----------------------------------;

" Might need to install coc-snippets and set the snippets
" directory using coc command `snippets.openSnippetFiles`
Plug 'SirVer/ultisnips' | Plug 'honza/vim-snippets'
let g:UltiSnipsExpandTrigger = '<Nop>'
imap <tab> <Plug>(coc-snippets-expand)

Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
let g:fzf_buffers_jump = 1
let g:fzf_preview_window = ['right:50%', 'ctrl-/']
let g:fzf_history_dir = '~/.local/share/fzf-history'
let $FZF_DEFAULT_COMMAND = 'fd -H'
let $FZF_DEFAULT_OPTS = '--exact --reverse'
Plug 'stsewd/fzf-checkout.vim'
let g:fzf_checkout_git_options = '--sort=-committerdate'
let g:fzf_tag_actions = {
\ 'checkout': {'execute': '!{git} -C {cwd} checkout {branch}'},
\}

Plug 'tpope/vim-projectionist'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-eunuch'

Plug 'jiangmiao/auto-pairs'
Plug 'thinca/vim-quickrun'
Plug 'wellle/targets.vim'
Plug 'preservim/nerdtree'
Plug 'mbbill/undotree'

Plug 'easymotion/vim-easymotion'
map s <Plug>(easymotion-s)

Plug 'vim-test/vim-test'
let test#strategy = "vimterminal"

" Buffers and tabs managemenet
Plug 'moll/vim-bbye'
Plug 'gcmt/taboo.vim'
" Make sessions remember taboo tabs names
set sessionoptions+=tabpages,globals

"----------------------------------;
" Git wrappers                     ;
"----------------------------------;

Plug 'tpope/vim-fugitive'

Plug 'airblade/vim-gitgutter'
let g:gitgutter_grep = 'rg'

set updatetime=100

"----------------------------------;
" Language servers / others        ;
"----------------------------------;

" Might need to install coc-marketplace and search
" for extensions with :CocList marketplace
Plug 'neoclide/coc.nvim', {'branch': 'release'}

Plug 'vimwiki/vimwiki'
let g:vimwiki_global_ext = 0
let g:vimwiki_list = [{'path': '~/.vimwiki', 'auto_diary_index': 1}]

"----------------------------------;
" Language plugins                 ;
"----------------------------------;

Plug 'sheerun/vim-polyglot'

"----------------------------------;
" Styling                          ;
"----------------------------------;

set statusline=
set statusline+=%f
set statusline+=\ %r
set statusline+=%<
set statusline+=%=
set statusline+=%{GetBranch()}
set statusline+=%{GitStatus()}
set statusline+=\ %{expand(&filetype)}\ \\|
set statusline+=\ %{expand(&fileformat)}\ \\|
set statusline+=\ %{&fileencoding?&fileencoding:&encoding}\ \\|
set statusline+=\ %p%%\ \\|
set statusline+=\ %l:%v\ \\|

Plug 'ryanoasis/vim-devicons'

Plug 'sainnhe/gruvbox-material'
let g:gruvbox_material_background = 'hard'

call plug#end()

"---------------------------------------------------------------;
"                            Theme                              ;
"---------------------------------------------------------------;

" Improve colors on terminal
set termguicolors

colorscheme gruvbox-material
set background=dark

"---------------------------------------------------------------;
"                         Autocommands                          ;
"---------------------------------------------------------------;

augroup my_autocommands
  " Remove trailing whitespaces on write
  au BufWritePre * %s/\s\+$//e
  " Open help windows vertically splitted
  au FileType help wincmd L
  " Meaningful backup name, ex: filename@2015-04-05.14:59
  au BufWritePre * let &bex = '@' . strftime("%F.%H:%M")
augroup end

"---------------------------------------------------------------;
"                          Functions                            ;
"---------------------------------------------------------------;

function ShowDocumentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

function GetBranch()
  let head = FugitiveHead()
  return head != '' ? 'git:(' . head . ') ' : ''
endfunction

function! GitStatus()
  let [a,m,r] = GitGutterGetHunkSummary()
  let head = FugitiveHead()
  return head != '' ? printf('| +%d ~%d -%d |', a, m, r) : ''
endfunction

function CommandWithInput(command, prompt)
  let user_input = input(a:prompt)
  " If the user cancelled the prompt, just return
  if user_input == ''
    return
  endif
  execute a:command . ' ' . user_input
endfunction

function! ToggleQuickFix()
    if empty(filter(getwininfo(), 'v:val.quickfix'))
        copen
    else
        cclose
    endif
endfunction

function GoToDefinition()
  " TODO: Suppress coc warning if the filetype doesn't have lsp support
  if CocAction('jumpDefinition')
    norm <Plug>(coc-definition)
  else
    norm! gd
  endif
endfunction

"--------------------------------------------------------------;
"                          Commands                            ;
"--------------------------------------------------------------;

command TabOpen call CommandWithInput(':TabooOpen', 'Tab name: ')
command TabRename call CommandWithInput(':TabooRename', 'Tab name: ')

" Add `:Fold` command to fold current buffer.
command! -nargs=? Fold :call CocAction('fold', <f-args>)
" Add `:OR` command for organize imports of the current buffer.
command! -nargs=0 OR   :call CocAction('runCommand', 'editor.action.organizeImport')
command! -nargs=0 Prettier :CocCommand prettier.formatFile

"--------------------------------------------------------------;
"                           Macros                             ;
"--------------------------------------------------------------;

let @s="iStoredMacroSample\<Esc>"

"--------------------------------------------------------------;
"                          Mappings                            ;
"--------------------------------------------------------------;

let mapleader=" "

"----------------------------------;
" b - Buffer                       ;
"----------------------------------;

" Save and delete current buffer
nnoremap <silent> <Leader>bd :Bdelete<cr>

" Change buffers
nnoremap <silent> <Leader>bn :bnext<cr>
nnoremap <silent> <Leader>bp :bprev<cr>
nnoremap <silent> <Leader>bu :UndotreeToggle<cr>

"----------------------------------;
" t - Tabs / Testing               ;
"----------------------------------;

nnoremap <Leader>to :TabOpen<cr>
nnoremap <Leader>tr :TabRename<cr>
nnoremap <Leader>tq :tabclose<cr>

" Swap tabs
" TODO: Add count support
nnoremap <silent><Leader>t< :execute "tabmove" tabpagenr() - 2 <CR>
nnoremap <silent><Leader>t> :execute "tabmove" tabpagenr() + 1 <CR>

nnoremap <Leader>ts :TestSuite<cr>
nnoremap <Leader>tn :TestNearest<cr>
nnoremap <Leader>tf :TestFile<cr>
nnoremap <Leader>tv :TestVisit<cr>
nnoremap <Leader>tl :TestLast<cr>

"----------------------------------;
" s - Session                      ;
"----------------------------------;
" TODO: Restore terminals

nnoremap <silent> <Leader>st :Startify<cr>
nnoremap <silent> <Leader>ss :SSave<cr>
nnoremap <silent> <Leader>sl :SLoad<cr>
nnoremap <silent> <Leader>sd :SDelete<cr>
nnoremap <silent> <Leader>sc :SClose<CR>

"----------------------------------;
" f - Find                         ;
"----------------------------------;

" Find files
nnoremap <silent> <Leader>ff :Files<cr>
" Find git files
nnoremap <silent> <Leader>fg :GFiles<cr>
" Find commits
nnoremap <silent> <Leader>fc :Commits<cr>
" Find buffers
nnoremap <silent> <Leader>fb :Buffers<cr>
" Find theme
nnoremap <silent> <Leader>ft :Colors<cr>
" Find windows
nnoremap <silent> <Leader>fw :Windows<cr>
" Find substring in files
nnoremap <silent> <Leader>fs :Rg<cr>
" Find mappings
nnoremap <silent> <Leader>fm :Maps<cr>
" Find marks
nnoremap <silent> <Leader>fM :Marks<cr>
" Find in help tags
nnoremap <silent> <Leader>fh :Helptags<cr>

"----------------------------------;
" p - Project                      ;
"----------------------------------;

" Open alternative projectionist file on vertical split
nnoremap <silent> <Leader>pa :AV<cr>

"----------------------------------;
" g - Git                          ;
"----------------------------------;

" Git status
nnoremap <silent> <Leader>gs :G<cr>
" Git diff
nnoremap <silent> <Leader>gd :Gvdiffsplit<cr>
" Git log
nnoremap <silent> <Leader>gl :Gclog<cr>
" Search and manage git branches
nnoremap <silent> <Leader>gb :GBranches<cr>
" Search and manage git tags
nnoremap <silent> <Leader>gt :GTags<cr>

" Select sides when resolving merge conflicts
nnoremap <silent> <Leader>gf :diffget //2<cr>
nnoremap <silent> <Leader>gj :diffget //3<cr>

"----------------------------------;
" c - Conquer of completion        ;
"----------------------------------;

" GoTo code navigation.
nmap <silent> gd :call GoToDefinition()<cr>
nmap <silent> <Leader>cgy <Plug>(coc-type-definition)
nmap <silent> <Leader>cgi <Plug>(coc-implementation)
nmap <silent> <Leader>cgr <Plug>(coc-references)

" Coc format
vmap <leader>cf <Plug>(coc-format-selected)
nmap <leader>cf :call CocAction('format')<cr>

" Symbol renaming.
nmap <leader>cr  <Plug>(coc-rename)

" Use `[g` and `]g` to navigate diagnostics
" Use `:CocDiagnostics` to get all diagnostics of current buffer in location list.
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" Remap keys for applying codeAction to the current buffer.
nmap <leader>cac  <Plug>(coc-codeaction)
" Coc fix
nmap <leader>cq  <Plug>(coc-fix-current)

" Show all diagnostics.
nnoremap <silent><nowait> <Leader>cd  :<C-u>CocList diagnostics<cr>
" Find symbol of current document.
nnoremap <silent><nowait> <Leader>co  :<C-u>CocList outline<cr>

" Manage extensions.
nnoremap <silent><nowait> <Leader>ce :<C-u>CocList extensions<cr>
" Show commands.
nnoremap <silent><nowait> <Leader>cc :<C-u>CocList commands<cr>

" Search workspace symbols.
nnoremap <silent><nowait> <Leader>cs :<C-u>CocList -I symbols<cr>
" Do default action for next item.
nnoremap <silent><nowait> <Leader>j  :<C-u>CocNext<CR>
" Do default action for previous item.
nnoremap <silent><nowait> <Leader>k  :<C-u>CocPrev<CR>
" Resume latest coc list.
" TODO: find mappings to those
" nnoremap <silent><nowait> <Leader>p  :<C-u>CocListResume<CR>

"----------------------------------;
" v - Vim utils                    ;
"----------------------------------;

" Reload vimrc
nnoremap <silent> <Leader>vr :source ~/.vim/vimrc<cr>
" QUIT vim
nnoremap <silent> <Leader>vQ :quitall!<cr>
" Install plugins
nnoremap <silent> <Leader>vi :PlugInstall<cr>
" Clean unused
nnoremap <silent> <Leader>vc :PlugClean<cr>
" Update plugins
nnoremap <silent> <Leader>vu :PlugUpdate<cr>

"----------------------------------;
" Others                           ;
"----------------------------------;

" Quick run
nnoremap <Leader>qr :QuickRun<cr>

" Quickfix list mappings
nnoremap <silent> <Leader>qt :call ToggleQuickFix()<cr>
nnoremap [q :cp<cr>
nnoremap ]q :cn<cr>

"----------------------------------;
" Other mappings                   ;
"----------------------------------;

nnoremap <silent> zs :update<cr>

nnoremap <silent> <C-n> :NERDTreeToggle<CR>

" Use K to show documentation in preview window.
nnoremap <silent> K :call ShowDocumentation()<CR>

" Repeat last command
noremap <C-p> @:

" Repeat last macro
nnoremap Q @@

" Go to first character in line
noremap H ^

" Move one character to left on insert mode
" useful for getting out of (), for example.
inoremap <C-e> <Esc>la

" TODO: Don't wait after pressing <c-w> on fzf buffers
" Open a terminal in a vertical split
" nnoremap <C-w>t :vert term ++kill=kill<cr>

" Shorthand for runnning ctrl-w in the vim terminal
" tnoremap <C-w>n <C-\><C-w>

"--------------------------------------------------------------;
"                        Text objects                          ;
"--------------------------------------------------------------;

" Map function and class text objects
" NOTE: Requires 'textDocument.documentSymbol' support from the language server.
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" Gitgutter text objects
omap ih <Plug>(GitGutterTextObjectInnerPending)
omap ah <Plug>(GitGutterTextObjectOuterPending)
xmap ih <Plug>(GitGutterTextObjectInnerVisual)
xmap ah <Plug>(GitGutterTextObjectOuterVisual)

" Applying codeAction to the selected region.
" Example: `<leader>caap` for current paragraph
xmap <leader>ca  <Plug>(coc-codeaction-selected)
nmap <leader>ca  <Plug>(coc-codeaction-selected)

"--------------------------------------------------------------;
"                            Misc                              ;
"--------------------------------------------------------------;

set undofile                " Save undos after file closes
set undodir=~/.vim/undo     " Where to save undo histories
set undolevels=1000         " How many undos
" Some coc language servers have issues with backup files, see #649.
set backup
" Where to store backups
set backupdir=~/.vim/backup
" Make backup before overwriting the current buffer
set writebackup
" Overwrite the original backup file
set backupcopy=yes
set lazyredraw
" Show relative numbers mantaining the current line number
set number relativenumber
" This might be slow on some terminal emulators
set cursorline
set splitright
set scrolloff=5
set noswapfile
set wildmode=longest:list,full
set linebreak
set tabstop=2 shiftwidth=2 expandtab " Set tabs to 2 spaces
set smartcase ignorecase hlsearch " Search options
set foldmethod=indent " Fold based on indention levels.
set foldnestmax=3 " Only fold up to three nested levels.
set nofoldenable " Disable folding by default
set showcmd
set colorcolumn=80 " 80 character ruler
" Ignore case on wildmenu completion
set wildignorecase
" TextEdit might fail if hidden is not set.
set hidden
" Give more space for displaying messages.
set cmdheight=2
" Don't pass messages to |ins-completion-menu|.
set shortmess+=c
set spelllang=pt,en
" Show indent line on code with tabs
set listchars=tab:\|\ "
set list
set textwidth=80

" Kitty helper (https://sw.kovidgoyal.net/kitty/faq.html#id3)
let &t_ut=''

"Mode Settings
let &t_SI.="\e[5 q" "SI = INSERT mode
let &t_SR.="\e[4 q" "SR = REPLACE mode
let &t_EI.="\e[1 q" "EI = NORMAL mode (ELSE)

"Cursor settings:
"  1 -> blinking block
"  2 -> solid block
"  3 -> blinking underscore
"  4 -> solid underscore
"  5 -> blinking vertical bar
"  6 -> solid vertical bar

"--------------------------------------------------------------;
"                   Conquer of Completion                      ;
"--------------------------------------------------------------;

" Use tab for trigger completion with characters ahead and navigate.
" NOTE: Use command ':verbose imap <tab>' to make sure tab is not mapped by
" other plugin before putting this into your config.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use <c-space> to trigger completion.
if has('nvim')
  inoremap <silent><expr> <c-space> coc#refresh()
else
  inoremap <silent><expr> <c-@> coc#refresh()
endif

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
      \: "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

augroup coc_autocommands
  autocmd!
  " Highlight the symbol and its references when holding the cursor.
  autocmd CursorHold * silent call CocActionAsync('highlight')
  " Setup formatexpr specified filetype(s).
  autocmd FileType typescript,json setl formatexpr=CocAction('formatSelected')
  " Update signature help on jump placeholder.
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup end

" Remap <C-f> and <C-b> for scroll float windows/popups.
if has('nvim-0.4.0') || has('patch-8.2.0750')
  nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
  inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
  inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
  vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
endif

" Use CTRL-S for selections ranges.
" Requires 'textDocument/selectionRange' support of language server.
nmap <silent> <C-s> <Plug>(coc-range-select)
xmap <silent> <C-s> <Plug>(coc-range-select)

